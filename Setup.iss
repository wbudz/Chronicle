; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Chronicle"
#define MyAppVersion "3.2.4"
#define MyAppPublisher "Witold Budziszewski"
#define MyAppURL "https://codeofwar.wbudziszewski.pl/"
#define MyAppExeName "Chronicle.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{D95CB3F0-5619-4E9B-BD6C-9DF12CCF3A99}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputDir=C:\Users\Witold\Documents\Programowanie\Paradox\Chronicle3\output
OutputBaseFilename=Chronicle_324
SetupIconFile=C:\Users\Witold\Documents\Programowanie\Paradox\Chronicle3\Chronicle\chronicle.ico
Compression=lzma2/ultra64
SolidCompression=yes
ArchitecturesInstallIn64BitMode = x64
ChangesAssociations=Yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[InstallDelete]
Type: filesandordirs; Name: "{app}\Script\Chronicle.Default"

[Dirs]
Name: "{app}\Script"; Permissions: users-modify
Name: "{app}\Script\Chronicle.Default"; Permissions: users-modify

[Files]
Source: "C:\Users\Witold\Documents\Programowanie\Paradox\Chronicle3\Chronicle\bin\Release\Chronicle.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Witold\Documents\Programowanie\Paradox\Chronicle3\Chronicle\bin\Release\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Registry]
Root: HKCR; Subkey: ".aar"; ValueType: string; ValueName: ""; ValueData: "AARFile"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "AARFile"; ValueType: string; ValueName: ""; ValueData: "Chronicle AAR File"; Flags: uninsdeletekey
Root: HKCR; Subkey: "AARFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\chronicle_file.ico"
Root: HKCR; Subkey: "AARFile\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Chronicle.EXE"" ""%1"""

[UninstallDelete]
;Type: filesandordirs; Name: "{userappdata}\Chronicle"
Type: filesandordirs; Name: "{localappdata}\Chronicle"

[Code]
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
mRes : integer;
entry: String;
begin
  case CurUninstallStep of
    usUninstall:
      begin
        mRes := MsgBox('Do you want to remove user files?', mbConfirmation, MB_YESNO or MB_DEFBUTTON2)
        entry := ExpandConstant('{userappdata}') + '\Chronicle\';
        if mRes = IDYES then
          begin
            if DirExists(entry) then begin
             DelTree(entry, true, true, true);
            end 
          End
      end;
  end;
end;